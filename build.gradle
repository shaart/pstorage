plugins {
    id "org.springframework.boot" version "3.0.2"
    id "java"
    id "application"
    id "checkstyle"
    id "io.spring.dependency-management" version "1.1.0"
    id "org.openjfx.javafxplugin" version "0.0.13"
}

def majorVersion = 0
def minorVersion = 1
def fixVersion = 0
version = "$majorVersion.$minorVersion.$fixVersion" as Object

group = "com.github.shaart"
mainClassName = "shaart.pstorage.PstorageApplication"

java {
    sourceCompatibility = "17"
    targetCompatibility = "17"
}

jar {
    enabled = false
}

javafx {
    version = "17"
    modules = ["javafx.controls", "javafx.fxml", "javafx.swing"]
    configuration = 'compileOnly'
}

checkstyle {
    toolVersion "10.7.0"
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source = "src/main/java"
}
checkstyleTest {
    source = "src/test/java"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    lombokVersion = "1.18.26"
    liquibaseVersion = "3.7.0"
}
dependencies {
    // Security
    implementation "org.springframework.boot:spring-boot-starter-security"

    // Data
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    runtimeOnly "com.h2database:h2"
    implementation "org.liquibase:liquibase-core:$liquibaseVersion"

    // Annotations
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Tests
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
}

tasks.withType(Checkstyle) {
    maxWarnings 0
    maxErrors 0
    reports {
        xml.required = false
        html.required = true
        html.stylesheet resources.text.fromFile("config/xsl/checkstyle-noframes-severity-sorted.xsl")
    }
}
